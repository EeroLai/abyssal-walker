class_name MeleeEffect
extends Node2D

var _color: Color = Color.WHITE
var _radius: float = 50.0
var _is_circle: bool = false
var _cone_angle_deg: float = 102.0


func setup(
	attack_range: float,
	angle_toward_target: float,
	eff_color: Color,
	is_circle: bool = false,
	cone_angle_deg: float = 102.0
) -> void:
	_radius = attack_range * 0.75
	_color = eff_color
	_is_circle = is_circle
	_cone_angle_deg = cone_angle_deg
	rotation = angle_toward_target
	queue_redraw()

	var tween := create_tween()
	tween.tween_property(self, "modulate:a", 0.0, 0.2)
	if _is_circle:
		tween.parallel().tween_property(self, "rotation", rotation + TAU * 0.75, 0.2)
	tween.tween_callback(queue_free)


func _draw() -> void:
	if _is_circle:
		_draw_circle_slash()
		return

	_draw_cone_slash()


func _draw_cone_slash() -> void:
	var half_angle := deg_to_rad(clampf(_cone_angle_deg, 20.0, 170.0) * 0.5)
	var arc_start := -half_angle
	var arc_end :=  half_angle
	var segments := 10
	var step := (arc_end - arc_start) / segments

	for i in range(segments):
		var a1 := arc_start + i * step
		var a2 := arc_start + (i + 1) * step
		var p1_outer := Vector2.from_angle(a1) * _radius
		var p2_outer := Vector2.from_angle(a2) * _radius
		var p1_inner := p1_outer * 0.5
		var p2_inner := p2_outer * 0.5

		draw_line(p1_outer, p2_outer, _color, 3.0)
		draw_line(p1_inner, p2_inner, Color(_color.r, _color.g, _color.b, 0.5), 2.0)

	# 弧形兩端封口線
	var edge_start := Vector2.from_angle(arc_start)
	var edge_end   := Vector2.from_angle(arc_end)
	draw_line(edge_start * _radius * 0.5, edge_start * _radius, _color, 2.0)
	draw_line(edge_end   * _radius * 0.5, edge_end   * _radius, _color, 2.0)


func _draw_circle_slash() -> void:
	draw_arc(Vector2.ZERO, _radius, 0.0, TAU, 40, _color, 3.0, true)
	draw_arc(Vector2.ZERO, _radius * 0.66, 0.0, TAU, 32, Color(_color.r, _color.g, _color.b, 0.55), 2.0, true)

	for i in range(6):
		var a := (TAU / 6.0) * i
		var inner := Vector2.from_angle(a) * (_radius * 0.4)
		var outer := Vector2.from_angle(a + 0.2) * _radius
		draw_line(inner, outer, Color(_color.r, _color.g, _color.b, 0.7), 2.0)
