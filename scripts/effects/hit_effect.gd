class_name HitEffect
extends Node2D

const PARTICLE_COUNT := 7

var _color: Color = Color.WHITE
var _particles: Array[Dictionary] = []


func setup(eff_color: Color) -> void:
	_color = eff_color

	for i in range(PARTICLE_COUNT):
		_particles.append({
			"dir":      Vector2.from_angle(randf() * TAU),
			"speed":    randf_range(40.0, 130.0),
			"size":     randf_range(2.0, 5.0),
			"lifetime": randf_range(0.12, 0.28),
			"elapsed":  0.0,
		})

	queue_redraw()

	var tween := create_tween()
	tween.tween_property(self, "modulate:a", 0.0, 0.28)
	tween.tween_callback(queue_free)


func _process(delta: float) -> void:
	for p in _particles:
		p.elapsed += delta
	queue_redraw()


func _draw() -> void:
	for p in _particles:
		if p.elapsed >= p.lifetime:
			continue
		var t: float = p.elapsed / p.lifetime
		var pos: Vector2 = p.dir * p.speed * p.elapsed
		var size: float = p.size * (1.0 - t)
		draw_circle(pos, size, _color)
