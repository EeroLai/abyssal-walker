class_name Affix
extends Resource

@export var id: String = ""
@export var display_name: String = ""
@export var group: String = ""
@export var affix_type: StatTypes.AffixType = StatTypes.AffixType.PREFIX
@export var tier: int = 1
@export var allowed_slots: Array[StatTypes.EquipmentSlot] = []

# 屬性修改器列表
@export var stat_modifiers: Array[StatModifier] = []


func get_description() -> String:
	var lines: Array[String] = []
	for modifier in stat_modifiers:
		lines.append(modifier.get_description())
	return "\n".join(lines)


func apply_to_stats(stats: StatContainer, source: String) -> void:
	for modifier in stat_modifiers:
		modifier.apply_to_stats(stats, source)


func remove_from_stats(stats: StatContainer, source: String) -> void:
	for modifier in stat_modifiers:
		modifier.remove_from_stats(stats, source)


func can_apply_to_slot(slot: StatTypes.EquipmentSlot) -> bool:
	return allowed_slots.is_empty() or slot in allowed_slots


func duplicate_affix() -> Affix:
	var new_affix := Affix.new()
	new_affix.id = id
	new_affix.display_name = display_name
	new_affix.group = group
	new_affix.affix_type = affix_type
	new_affix.tier = tier
	new_affix.allowed_slots = allowed_slots.duplicate()
	new_affix.stat_modifiers = []
	for modifier in stat_modifiers:
		new_affix.stat_modifiers.append(modifier.duplicate_modifier())
	return new_affix
