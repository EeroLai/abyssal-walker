class_name EquipmentData
extends Resource

@export var id: String = ""
@export var display_name: String = ""
@export var item_level: int = 1
@export var slot: StatTypes.EquipmentSlot
@export var rarity: StatTypes.Rarity = StatTypes.Rarity.WHITE

@export var weapon_type: StatTypes.WeaponType
@export var off_hand_type: StatTypes.OffHandType

@export var base_stats: Array[StatModifier] = []
@export var prefixes: Array[Affix] = []
@export var suffixes: Array[Affix] = []
@export var legendary_affix: Affix = null
@export var crafting_state: Dictionary = {}


func get_unique_id() -> String:
	return "%s_%d" % [id, get_instance_id()]


func get_all_affixes() -> Array[Affix]:
	var all: Array[Affix] = []
	if legendary_affix:
		all.append(legendary_affix)
	all.append_array(prefixes)
	all.append_array(suffixes)
	return all


func get_total_affix_count() -> int:
	var count := prefixes.size() + suffixes.size()
	if legendary_affix:
		count += 1
	return count


func apply_to_stats(stats: StatContainer) -> void:
	var source := get_unique_id()
	for modifier in base_stats:
		modifier.apply_to_stats(stats, source)
	for affix in get_all_affixes():
		affix.apply_to_stats(stats, source)


func remove_from_stats(stats: StatContainer) -> void:
	var source := get_unique_id()
	for modifier in base_stats:
		modifier.remove_from_stats(stats, source)
	for affix in get_all_affixes():
		affix.remove_from_stats(stats, source)


func get_display_color() -> Color:
	return StatTypes.RARITY_COLORS.get(rarity, Color.WHITE)


func get_tooltip() -> String:
	var lines: Array[String] = []
	lines.append("[color=%s]%s[/color]" % [get_display_color().to_html(false), display_name])
	lines.append("%s | %s" % [_get_rarity_name(), StatTypes.SLOT_NAMES.get(slot, "未知部位")])
	if Input.is_key_pressed(KEY_ALT):
		lines.append("iLvl %d" % item_level)
	lines.append("")

	for modifier in base_stats:
		lines.append(modifier.get_description())

	if not get_all_affixes().is_empty():
		lines.append("")

	for affix in prefixes:
		var prefix_label := "[color=#8888ff]前綴 T%d[/color]" % _display_tier(affix.tier)
		if affix.stat_modifiers.is_empty():
			lines.append(prefix_label)
		else:
			for i in range(affix.stat_modifiers.size()):
				var mod: StatModifier = affix.stat_modifiers[i]
				if i == 0:
					lines.append("%s  %s" % [prefix_label, mod.get_description()])
				else:
					lines.append("  %s" % mod.get_description())
	for affix in suffixes:
		var suffix_label := "[color=#88ff88]後綴 T%d[/color]" % _display_tier(affix.tier)
		if affix.stat_modifiers.is_empty():
			lines.append(suffix_label)
		else:
			for i in range(affix.stat_modifiers.size()):
				var mod: StatModifier = affix.stat_modifiers[i]
				if i == 0:
					lines.append("%s  %s" % [suffix_label, mod.get_description()])
				else:
					lines.append("  %s" % mod.get_description())
	if legendary_affix:
		lines.append("[color=#ffb347]傳奇詞綴[/color]")
		lines.append(legendary_affix.get_description())

	return "\n".join(lines)


func _get_rarity_name() -> String:
	match rarity:
		StatTypes.Rarity.WHITE: return "普通"
		StatTypes.Rarity.BLUE: return "魔法"
		StatTypes.Rarity.YELLOW: return "稀有"
		StatTypes.Rarity.ORANGE: return "傳奇"
		_: return "未知"


func is_weapon() -> bool:
	return slot == StatTypes.EquipmentSlot.MAIN_HAND


func is_off_hand() -> bool:
	return slot == StatTypes.EquipmentSlot.OFF_HAND


func _display_tier(raw_tier: int) -> int:
	var clamped := clampi(raw_tier, 1, 5)
	return 6 - clamped
