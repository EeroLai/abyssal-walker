class_name ItemGenerator
extends RefCounted

static var _ilvl100_no_special_streak: int = 0
const RISK_HIGH_TIER_THRESHOLD: int = 80

## 裝備生成器

## 生成指定稀有度的裝備
static func generate_equipment(
	base_id: String,
	rarity: StatTypes.Rarity,
	floor_level: int = 1,
	risk_tier_bonus: int = 0,
	risk_score_bonus: int = 0
) -> EquipmentData:
	var base_data: Dictionary = DataManager.get_equipment_base(base_id)
	if base_data.is_empty():
		push_error("[ItemGenerator] Unknown equipment base: %s" % base_id)
		return null

	var equipment := EquipmentData.new()
	equipment.id = base_id
	equipment.display_name = base_data.get("display_name", base_id)
	equipment.item_level = _floor_to_item_level(floor_level)
	equipment.rarity = rarity

	# 設定欄位
	var slot_str: String = base_data.get("slot", "")
	equipment.slot = _string_to_slot(slot_str)

	# 設定武器類型
	if base_data.has("weapon_type"):
		equipment.weapon_type = _string_to_weapon_type(base_data.weapon_type)
	if base_data.has("off_hand_type"):
		equipment.off_hand_type = _string_to_offhand_type(base_data.off_hand_type)

	# 生成基礎屬性
	equipment.base_stats = _generate_base_stats(base_data)

	# 生成詞綴
	var item_level: int = _floor_to_item_level(floor_level)
	var affix_count := _get_affix_count(rarity, item_level, risk_score_bonus)
	_generate_affixes(equipment, affix_count, item_level, risk_tier_bonus)

	return equipment


## 生成隨機裝備
static func generate_random_equipment(
	slot: StatTypes.EquipmentSlot,
	floor_level: int = 1
) -> EquipmentData:
	# 決定稀有度
	var risk_score_bonus: int = _get_drop_risk_score()
	var rarity := _roll_rarity(floor_level, risk_score_bonus)

	# 找該欄位的基底
	var base_id := _pick_random_base_for_slot(slot)
	if base_id.is_empty():
		return null

	var risk_tier_bonus: int = _get_drop_risk_tier_bonus()
	var equipment := generate_equipment(
		base_id, rarity, floor_level, risk_tier_bonus, risk_score_bonus
	)
	if equipment == null:
		return null
	_apply_ilvl100_pity_if_needed(equipment)
	return equipment


static func _generate_base_stats(base_data: Dictionary) -> Array[StatModifier]:
	var stats: Array[StatModifier] = []

	var base_stats: Array = base_data.get("base_stats", [])
	for stat_data: Dictionary in base_stats:
		var modifier: StatModifier = StatModifier.new()
		modifier.stat = _string_to_stat(stat_data.get("stat", ""))
		modifier.value = stat_data.get("value", 0.0)

		var type_str: String = stat_data.get("type", "flat")
		modifier.modifier_type = (
			StatModifier.ModifierType.PERCENT
			if type_str == "percent"
			else StatModifier.ModifierType.FLAT
		)

		stats.append(modifier)

	return stats


static func _generate_affixes(
	equipment: EquipmentData,
	total_count: int,
	item_level: int,
	risk_tier_bonus: int = 0
) -> void:
	var counts: Vector2i = _get_prefix_suffix_counts(equipment.rarity, total_count)
	var prefix_count: int = counts.x
	var suffix_count: int = counts.y
	var blocked_groups: Array[String] = []

	var available_prefixes: Array = DataManager.get_affixes_for_slot(
		equipment.slot, StatTypes.AffixType.PREFIX
	)
	for i in range(prefix_count):
		available_prefixes = _filter_affix_pool(available_prefixes, blocked_groups)
		if available_prefixes.is_empty():
			break
		var affix: Affix = _pick_and_generate_affix(available_prefixes, item_level, risk_tier_bonus)
		if affix:
			equipment.prefixes.append(affix)
			if affix.group != "":
				blocked_groups.append(affix.group)
			available_prefixes = available_prefixes.filter(
				func(a: Dictionary) -> bool: return str(a.get("id", "")) != affix.id
			)

	var available_suffixes: Array = DataManager.get_affixes_for_slot(
		equipment.slot, StatTypes.AffixType.SUFFIX
	)
	for i in range(suffix_count):
		available_suffixes = _filter_affix_pool(available_suffixes, blocked_groups)
		if available_suffixes.is_empty():
			break
		var affix: Affix = _pick_and_generate_affix(available_suffixes, item_level, risk_tier_bonus)
		if affix:
			equipment.suffixes.append(affix)
			if affix.group != "":
				blocked_groups.append(affix.group)
			available_suffixes = available_suffixes.filter(
				func(a: Dictionary) -> bool: return str(a.get("id", "")) != affix.id
			)


static func _filter_affix_pool(pool: Array, blocked_groups: Array[String]) -> Array:
	if blocked_groups.is_empty():
		return pool
	return pool.filter(
		func(a: Dictionary) -> bool:
			var group: String = str(a.get("group", ""))
			return group == "" or not blocked_groups.has(group)
	)


static func _get_prefix_suffix_counts(rarity: StatTypes.Rarity, total_count: int) -> Vector2i:
	var total: int = maxi(total_count, 0)
	if total == 0:
		return Vector2i.ZERO

	match rarity:
		StatTypes.Rarity.BLUE:
			# Blue: 1 affix can be either prefix/suffix; 2 affixes must be 1+1.
			if total <= 1:
				return Vector2i(1, 0) if randf() < 0.5 else Vector2i(0, 1)
			return Vector2i(1, 1)
		StatTypes.Rarity.YELLOW:
			# Yellow: 3~6 total, with per-side cap 3.
			var clamped_total := mini(total, 6)
			# Keep both sides non-empty (no all-prefix or all-suffix).
			var min_prefix := maxi(1, clamped_total - 3)
			var max_prefix := mini(3, clamped_total - 1)
			var prefix_count: int = randi_range(min_prefix, max_prefix)
			var suffix_count: int = clamped_total - prefix_count
			return Vector2i(prefix_count, suffix_count)
		_:
			var prefix_count_default := ceili(total / 2.0)
			return Vector2i(prefix_count_default, total - prefix_count_default)


static func _pick_and_generate_affix(
	available: Array,
	item_level: int,
	risk_tier_bonus: int = 0
) -> Affix:
	if available.is_empty():
		return null

	var tier_eligible: Array = available.filter(
		func(a: Dictionary) -> bool:
			return _has_available_tier(a, item_level)
	)
	if tier_eligible.is_empty():
		return null

	var affix_data: Dictionary = _pick_weighted_affix_data(tier_eligible)
	if affix_data.is_empty():
		return null

	var affix: Affix = Affix.new()
	affix.id = str(affix_data.get("id", ""))
	affix.display_name = str(affix_data.get("display_name", affix.id))
	affix.group = str(affix_data.get("group", ""))
	affix.affix_type = int(affix_data.get("affix_type", StatTypes.AffixType.PREFIX))

	# 選擇 tier（根據層數）
	var tiers: Array = affix_data.get("tiers", [])
	if tiers.is_empty():
		return null

	var available_tier_indices: Array[int] = []
	for idx in range(tiers.size()):
		var tier_data: Dictionary = tiers[idx]
		if item_level >= _get_tier_min_ilvl(tier_data, idx):
			available_tier_indices.append(idx)
	if available_tier_indices.is_empty():
		return null
	var tier_index: int = _pick_weighted_tier_index(tiers, available_tier_indices, risk_tier_bonus)
	var tier_data: Dictionary = tiers[tier_index]

	affix.tier = tier_data.get("tier", 1)

	# 生成修改器
	var modifiers_data: Array = tier_data.get("modifiers", [])
	for mod_data: Dictionary in modifiers_data:
		var modifier: StatModifier = StatModifier.new()
		modifier.stat = _string_to_stat(mod_data.get("stat", ""))

		var type_str: String = mod_data.get("type", "flat")
		modifier.modifier_type = (
			StatModifier.ModifierType.PERCENT
			if type_str == "percent"
			else StatModifier.ModifierType.FLAT
		)

		var min_val: float = mod_data.get("min", 0.0)
		var max_val: float = mod_data.get("max", 0.0)
		modifier.value = randf_range(min_val, max_val)

		affix.stat_modifiers.append(modifier)

	return affix


static func _pick_weighted_affix_data(available: Array) -> Dictionary:
	if available.is_empty():
		return {}

	var total_weight: float = 0.0
	for a in available:
		var entry: Dictionary = a
		total_weight += maxf(float(entry.get("weight", 100.0)), 0.0)

	if total_weight <= 0.0:
		return available[randi() % available.size()]

	var roll: float = randf() * total_weight
	var cumulative: float = 0.0
	for a in available:
		var entry: Dictionary = a
		cumulative += maxf(float(entry.get("weight", 100.0)), 0.0)
		if roll <= cumulative:
			return entry
	return available[available.size() - 1]


static func _pick_weighted_tier_index(
	tiers: Array,
	available_indices: Array[int],
	risk_tier_bonus: int = 0
) -> int:
	if available_indices.is_empty():
		return 0
	if available_indices.size() == 1:
		return available_indices[0]

	var total_weight: float = 0.0
	var weights: Array[float] = []
	for idx in available_indices:
		var tier_data: Dictionary = tiers[idx]
		var tier_num: int = int(tier_data.get("tier", idx + 1))
		var base_weight: float = maxf(float(tier_num), 1.0)
		var quality_weight: float = pow(base_weight, 1.0 + maxf(float(risk_tier_bonus), 0.0) * 0.18)
		var weight: float = maxf(quality_weight, 0.001)
		weights.append(weight)
		total_weight += weight

	if total_weight <= 0.0:
		return available_indices[randi() % available_indices.size()]

	var roll: float = randf() * total_weight
	var cumulative: float = 0.0
	for i in range(available_indices.size()):
		cumulative += weights[i]
		if roll <= cumulative:
			return available_indices[i]
	return available_indices[available_indices.size() - 1]


static func _has_available_tier(affix_data: Dictionary, item_level: int) -> bool:
	var tiers: Array = affix_data.get("tiers", [])
	for idx in range(tiers.size()):
		var tier_data: Dictionary = tiers[idx]
		if item_level >= _get_tier_min_ilvl(tier_data, idx):
			return true
	return false


static func _roll_weighted_yellow_affix_count() -> int:
	var roll := randf()
	if roll < 0.20:
		return 3
	elif roll < 0.50:
		return 4
	elif roll < 0.80:
		return 5
	return 6


static func _roll_weighted_yellow_affix_count_with_risk(risk_score_bonus: int) -> int:
	var intensity := clampf(
		float(risk_score_bonus - RISK_HIGH_TIER_THRESHOLD) / 20.0,
		0.0,
		1.0
	)
	var w3 := lerpf(0.20, 0.05, intensity)
	var w4 := lerpf(0.30, 0.15, intensity)
	var w5 := lerpf(0.30, 0.40, intensity)
	var w6 := lerpf(0.20, 0.40, intensity)
	var roll := randf()
	if roll < w3:
		return 3
	elif roll < w3 + w4:
		return 4
	elif roll < w3 + w4 + w5:
		return 5
	return 6


static func _apply_ilvl100_pity_if_needed(equipment: EquipmentData) -> void:
	if equipment == null:
		return
	if equipment.rarity != StatTypes.Rarity.YELLOW:
		return
	if equipment.item_level < 100:
		return

	if _contains_ilvl100_affix(equipment):
		_ilvl100_no_special_streak = 0
		return

	var pity_chance := 0.005
	if _ilvl100_no_special_streak >= 60:
		pity_chance += 0.015 * float(_ilvl100_no_special_streak - 59)
	pity_chance = clampf(pity_chance, 0.0, 1.0)

	if randf() < pity_chance:
		_force_add_ilvl100_affix(equipment)

	if _contains_ilvl100_affix(equipment):
		_ilvl100_no_special_streak = 0
	else:
		_ilvl100_no_special_streak += 1


static func _contains_ilvl100_affix(equipment: EquipmentData) -> bool:
	for affix: Affix in equipment.prefixes:
		if _is_ilvl100_affix_instance(affix):
			return true
	for affix: Affix in equipment.suffixes:
		if _is_ilvl100_affix_instance(affix):
			return true
	return false


static func _is_ilvl100_affix_instance(affix: Affix) -> bool:
	if affix == null:
		return false
	var affix_data: Dictionary = DataManager.get_affix(affix.id)
	if affix_data.is_empty():
		return false
	var tiers: Array = affix_data.get("tiers", [])
	for idx in range(tiers.size()):
		var tier_data: Dictionary = tiers[idx]
		if int(tier_data.get("tier", idx + 1)) == affix.tier:
			return _get_tier_min_ilvl(tier_data, idx) >= 100
	return false


static func _force_add_ilvl100_affix(equipment: EquipmentData) -> void:
	var existing_ids: Array[String] = []
	var blocked_groups: Array[String] = []
	for affix: Affix in equipment.get_all_affixes():
		existing_ids.append(affix.id)
		if affix.group != "":
			blocked_groups.append(affix.group)

	var candidate_types: Array[int] = []
	var prefix_pool := _build_ilvl100_pool_for_type(
		equipment.slot, StatTypes.AffixType.PREFIX, existing_ids, blocked_groups
	)
	if not prefix_pool.is_empty():
		candidate_types.append(StatTypes.AffixType.PREFIX)

	var suffix_pool := _build_ilvl100_pool_for_type(
		equipment.slot, StatTypes.AffixType.SUFFIX, existing_ids, blocked_groups
	)
	if not suffix_pool.is_empty():
		candidate_types.append(StatTypes.AffixType.SUFFIX)

	if candidate_types.is_empty():
		return

	var chosen_type: int = candidate_types[randi() % candidate_types.size()]
	var pool: Array = prefix_pool if chosen_type == StatTypes.AffixType.PREFIX else suffix_pool
	var affix: Affix = _pick_and_generate_affix(pool, equipment.item_level)
	if affix == null:
		return

	if chosen_type == StatTypes.AffixType.PREFIX:
		if equipment.prefixes.size() >= 3:
			equipment.prefixes[randi() % equipment.prefixes.size()] = affix
		else:
			equipment.prefixes.append(affix)
	else:
		if equipment.suffixes.size() >= 3:
			equipment.suffixes[randi() % equipment.suffixes.size()] = affix
		else:
			equipment.suffixes.append(affix)


static func _build_ilvl100_pool_for_type(
	slot: StatTypes.EquipmentSlot,
	affix_type: StatTypes.AffixType,
	existing_ids: Array[String],
	blocked_groups: Array[String]
) -> Array:
	var available: Array = DataManager.get_affixes_for_slot(slot, affix_type)
	available = available.filter(
		func(a: Dictionary) -> bool:
			var id: String = str(a.get("id", ""))
			if existing_ids.has(id):
				return false
			var group: String = str(a.get("group", ""))
			if group != "" and blocked_groups.has(group):
				return false
			var tiers: Array = a.get("tiers", [])
			for idx in range(tiers.size()):
				var tier_data: Dictionary = tiers[idx]
				if _get_tier_min_ilvl(tier_data, idx) >= 100:
					return true
			return false
	)
	return available


static func _roll_rarity(
	floor_level: int,
	risk_score_bonus: int = 0
) -> StatTypes.Rarity:
	var item_level: int = _floor_to_item_level(floor_level)
	var white_chance := 0.60
	var magic_chance := 0.32
	var rare_chance := 0.08

	if item_level >= 100:
		white_chance = 0.05
		magic_chance = 0.50
		rare_chance = 0.45
	elif item_level >= 70:
		white_chance = 0.15
		magic_chance = 0.50
		rare_chance = 0.35
	elif item_level >= 35:
		white_chance = 0.20
		magic_chance = 0.55
		rare_chance = 0.25
	else:
		white_chance = 0.45
		magic_chance = 0.45
		rare_chance = 0.10

	if risk_score_bonus >= RISK_HIGH_TIER_THRESHOLD:
		var risk_over: int = maxi(risk_score_bonus - RISK_HIGH_TIER_THRESHOLD, 0)
		var rare_boost := 0.10 + minf(float(risk_over) * 0.005, 0.10)
		var white_reduce := minf(white_chance * 0.75, rare_boost * 0.45)
		var magic_reduce := rare_boost - white_reduce
		white_chance = maxf(0.0, white_chance - white_reduce)
		magic_chance = maxf(0.0, magic_chance - magic_reduce)
		rare_chance = minf(0.95, rare_chance + rare_boost)

	var total_chance := white_chance + magic_chance + rare_chance
	if total_chance > 0.0:
		white_chance /= total_chance
		magic_chance /= total_chance
		rare_chance /= total_chance

	var roll := randf()
	if roll < white_chance:
		return StatTypes.Rarity.WHITE
	elif roll < white_chance + magic_chance:
		return StatTypes.Rarity.BLUE
	else:
		return StatTypes.Rarity.YELLOW


static func _get_affix_count(
	rarity: StatTypes.Rarity,
	_item_level: int = 1,
	risk_score_bonus: int = 0
) -> int:
	match rarity:
		StatTypes.Rarity.WHITE:
			return 0
		StatTypes.Rarity.BLUE:
			return randi_range(1, 2)
		StatTypes.Rarity.YELLOW:
			if risk_score_bonus >= RISK_HIGH_TIER_THRESHOLD:
				return _roll_weighted_yellow_affix_count_with_risk(risk_score_bonus)
			return _roll_weighted_yellow_affix_count()
		StatTypes.Rarity.ORANGE:
			return randi_range(4, 6)
		_:
			return 0


static func get_affix_max(rarity: StatTypes.Rarity) -> int:
	match rarity:
		StatTypes.Rarity.WHITE:
			return 0
		StatTypes.Rarity.BLUE:
			return 2
		StatTypes.Rarity.YELLOW:
			return 6
		StatTypes.Rarity.ORANGE:
			return 6
		_:
			return 0


static func reroll_affixes(equipment: EquipmentData, floor_level: int) -> void:
	equipment.prefixes.clear()
	equipment.suffixes.clear()
	var item_level: int = _floor_to_item_level(floor_level)
	var affix_count := _get_affix_count(equipment.rarity, item_level)
	_generate_affixes(equipment, affix_count, item_level)


static func add_random_affix(equipment: EquipmentData, floor_level: int) -> bool:
	var max_total := get_affix_max(equipment.rarity)
	var current_total := equipment.get_total_affix_count()
	if current_total >= max_total:
		return false

	var prefix_max := ceili(max_total / 2.0)
	var suffix_max := max_total - prefix_max
	var can_prefix := equipment.prefixes.size() < prefix_max
	var can_suffix := equipment.suffixes.size() < suffix_max

	var type_roll := randf()
	var want_prefix := can_prefix and (not can_suffix or type_roll < 0.5)

	if want_prefix:
		return _add_affix_of_type(equipment, StatTypes.AffixType.PREFIX, floor_level)
	else:
		return _add_affix_of_type(equipment, StatTypes.AffixType.SUFFIX, floor_level)


static func _add_affix_of_type(
	equipment: EquipmentData,
	affix_type: StatTypes.AffixType,
	floor_level: int
) -> bool:
	var available := DataManager.get_affixes_for_slot(equipment.slot, affix_type)
	if available.is_empty():
		return false

	var existing_ids: Array[String] = []
	var blocked_groups: Array[String] = []
	for affix: Affix in equipment.get_all_affixes():
		existing_ids.append(affix.id)
		if affix.group != "":
			blocked_groups.append(affix.group)

	available = available.filter(
		func(a: Dictionary) -> bool:
			return not existing_ids.has(a.id)
	)
	available = _filter_affix_pool(available, blocked_groups)

	if available.is_empty():
		return false

	var new_affix: Affix = _pick_and_generate_affix(available, _floor_to_item_level(floor_level))
	if new_affix == null:
		return false

	if affix_type == StatTypes.AffixType.PREFIX:
		equipment.prefixes.append(new_affix)
	else:
		equipment.suffixes.append(new_affix)

	return true


static func _floor_to_item_level(floor_level: int) -> int:
	return clampi(floor_level, 1, 100)


static func _get_drop_risk_tier_bonus() -> int:
	if GameManager != null and GameManager.has_method("get_risk_tier"):
		return int(GameManager.get_risk_tier())
	return 0


static func _get_drop_risk_score() -> int:
	if GameManager != null:
		return int(GameManager.risk_score)
	return 0


static func _get_tier_min_ilvl(tier_data: Dictionary, idx: int) -> int:
	if tier_data.has("min_ilvl"):
		return int(tier_data.get("min_ilvl", 1))

	var tier_num: int = int(tier_data.get("tier", idx + 1))
	match tier_num:
		1:
			return 1
		2:
			return 15
		3:
			return 30
		4:
			return 50
		5:
			return 70
		_:
			return 70 + maxi(tier_num - 5, 0) * 20


static func _pick_random_base_for_slot(slot: StatTypes.EquipmentSlot) -> String:
	var slot_str := _slot_to_string(slot)
	var matching_bases: Array = []

	for base_id in DataManager.equipment_bases.keys():
		var base_data: Dictionary = DataManager.equipment_bases[base_id]
		var base_slot: String = str(base_data.get("slot", ""))
		var is_ring_slot := slot == StatTypes.EquipmentSlot.RING_1 or slot == StatTypes.EquipmentSlot.RING_2
		if base_slot == slot_str or (is_ring_slot and base_slot == "ring"):
			matching_bases.append(base_id)

	if matching_bases.is_empty():
		return ""

	return WeightedRandom.pick_one(matching_bases)


# ===== 字串轉換輔助 =====

static func _string_to_slot(slot_str: String) -> StatTypes.EquipmentSlot:
	match slot_str:
		"main_hand": return StatTypes.EquipmentSlot.MAIN_HAND
		"off_hand": return StatTypes.EquipmentSlot.OFF_HAND
		"helmet": return StatTypes.EquipmentSlot.HELMET
		"armor": return StatTypes.EquipmentSlot.ARMOR
		"gloves": return StatTypes.EquipmentSlot.GLOVES
		"boots": return StatTypes.EquipmentSlot.BOOTS
		"belt": return StatTypes.EquipmentSlot.BELT
		"amulet": return StatTypes.EquipmentSlot.AMULET
		"ring", "ring_1": return StatTypes.EquipmentSlot.RING_1
		"ring_2": return StatTypes.EquipmentSlot.RING_2
		_: return StatTypes.EquipmentSlot.MAIN_HAND


static func _slot_to_string(slot: StatTypes.EquipmentSlot) -> String:
	match slot:
		StatTypes.EquipmentSlot.MAIN_HAND: return "main_hand"
		StatTypes.EquipmentSlot.OFF_HAND: return "off_hand"
		StatTypes.EquipmentSlot.HELMET: return "helmet"
		StatTypes.EquipmentSlot.ARMOR: return "armor"
		StatTypes.EquipmentSlot.GLOVES: return "gloves"
		StatTypes.EquipmentSlot.BOOTS: return "boots"
		StatTypes.EquipmentSlot.BELT: return "belt"
		StatTypes.EquipmentSlot.AMULET: return "amulet"
		StatTypes.EquipmentSlot.RING_1: return "ring_1"
		StatTypes.EquipmentSlot.RING_2: return "ring_2"
		_: return "unknown"


static func _string_to_weapon_type(type_str: String) -> StatTypes.WeaponType:
	match type_str:
		"sword": return StatTypes.WeaponType.SWORD
		"dagger": return StatTypes.WeaponType.DAGGER
		"bow": return StatTypes.WeaponType.BOW
		"wand": return StatTypes.WeaponType.WAND
		_: return StatTypes.WeaponType.SWORD


static func _string_to_offhand_type(type_str: String) -> StatTypes.OffHandType:
	match type_str:
		"talisman": return StatTypes.OffHandType.TALISMAN
		"warmark": return StatTypes.OffHandType.WARMARK
		"arcane": return StatTypes.OffHandType.ARCANE
		_: return StatTypes.OffHandType.TALISMAN


static func _string_to_stat(stat_str: String) -> StatTypes.Stat:
	match stat_str:
		"hp": return StatTypes.Stat.HP
		"atk": return StatTypes.Stat.ATK
		"atk_speed": return StatTypes.Stat.ATK_SPEED
		"move_speed": return StatTypes.Stat.MOVE_SPEED
		"def": return StatTypes.Stat.DEF
		"crit_rate": return StatTypes.Stat.CRIT_RATE
		"crit_dmg": return StatTypes.Stat.CRIT_DMG
		"final_dmg": return StatTypes.Stat.FINAL_DMG
		"phys_pen": return StatTypes.Stat.PHYS_PEN
		"elemental_pen": return StatTypes.Stat.ELEMENTAL_PEN
		"armor_shred": return StatTypes.Stat.ARMOR_SHRED
		"res_shred": return StatTypes.Stat.RES_SHRED
		"life_steal": return StatTypes.Stat.LIFE_STEAL
		"life_regen": return StatTypes.Stat.LIFE_REGEN
		"dodge": return StatTypes.Stat.DODGE
		"block_rate": return StatTypes.Stat.BLOCK_RATE
		"block_reduction": return StatTypes.Stat.BLOCK_REDUCTION
		"physical_dmg": return StatTypes.Stat.PHYSICAL_DMG
		"fire_dmg": return StatTypes.Stat.FIRE_DMG
		"ice_dmg": return StatTypes.Stat.ICE_DMG
		"lightning_dmg": return StatTypes.Stat.LIGHTNING_DMG
		"fire_res": return StatTypes.Stat.FIRE_RES
		"ice_res": return StatTypes.Stat.ICE_RES
		"lightning_res": return StatTypes.Stat.LIGHTNING_RES
		"all_res": return StatTypes.Stat.ALL_RES
		"burn_chance": return StatTypes.Stat.BURN_CHANCE
		"freeze_chance": return StatTypes.Stat.FREEZE_CHANCE
		"shock_chance": return StatTypes.Stat.SHOCK_CHANCE
		"bleed_chance": return StatTypes.Stat.BLEED_CHANCE
		"burn_dmg_bonus": return StatTypes.Stat.BURN_DMG_BONUS
		"freeze_duration_bonus": return StatTypes.Stat.FREEZE_DURATION_BONUS
		"shock_effect_bonus": return StatTypes.Stat.SHOCK_EFFECT_BONUS
		"bleed_dmg_bonus": return StatTypes.Stat.BLEED_DMG_BONUS
		"phys_to_fire": return StatTypes.Stat.PHYS_TO_FIRE
		"phys_to_ice": return StatTypes.Stat.PHYS_TO_ICE
		"phys_to_lightning": return StatTypes.Stat.PHYS_TO_LIGHTNING
		"fire_to_ice": return StatTypes.Stat.FIRE_TO_ICE
		"ice_to_lightning": return StatTypes.Stat.ICE_TO_LIGHTNING
		"drop_rate": return StatTypes.Stat.DROP_RATE
		"drop_quality": return StatTypes.Stat.DROP_QUALITY
		"pickup_range": return StatTypes.Stat.PICKUP_RANGE
		_: return StatTypes.Stat.HP
