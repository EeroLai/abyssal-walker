class_name CoreBoard
extends RefCounted

signal board_changed

const MAX_SLOTS: int = 8
const LOAD_CAPACITY: int = Constants.BASE_LOAD_CAPACITY

# 已裝備的模組（最多 8 個）
var slots: Array[Module] = []

# 當前負載消耗
var current_load: int = 0


func get_used_load() -> int:
	return current_load


func get_remaining_load() -> int:
	return LOAD_CAPACITY - current_load


func can_equip(module: Module) -> bool:
	if module == null:
		return false
	if slots.size() >= MAX_SLOTS:
		return false
	if current_load + module.load_cost > LOAD_CAPACITY:
		return false
	# 不能裝備相同模組兩次
	for m in slots:
		if m.id == module.id:
			return false
	return true


func equip(module: Module, stats: StatContainer) -> bool:
	if not can_equip(module):
		return false
	slots.append(module)
	current_load += module.load_cost
	module.apply_to_stats(stats)
	board_changed.emit()
	return true


func unequip(module: Module, stats: StatContainer) -> bool:
	var idx := slots.find(module)
	if idx == -1:
		return false
	slots.remove_at(idx)
	current_load -= module.load_cost
	module.remove_from_stats(stats)
	board_changed.emit()
	return true


func unequip_at(index: int, stats: StatContainer) -> Module:
	if index < 0 or index >= slots.size():
		return null
	var module := slots[index]
	slots.remove_at(index)
	current_load -= module.load_cost
	module.remove_from_stats(stats)
	board_changed.emit()
	return module


func has_module(module_id: String) -> bool:
	for m in slots:
		if m.id == module_id:
			return true
	return false


func get_slot_count() -> int:
	return slots.size()
